; SPIR-V
; Version: 1.0
; Schema: 0
                 OpCapability Shader
                 OpCapability VariablePointers
                 OpExtension "SPV_KHR_storage_buffer_storage_class"
                 OpExtension "SPV_KHR_variable_pointers"
                 OpMemoryModel Logical GLSL450
                 OpEntryPoint GLCompute %18 "test_integer_arithmetic" %12
                 OpSource OpenCL_C 120
                 OpDecorate %13 SpecId 0
                 OpDecorate %14 SpecId 1
                 OpDecorate %15 SpecId 2
                 OpDecorate %3 ArrayStride 4
                 OpMemberDecorate %4 0 Offset 0
                 OpDecorate %4 Block
                 OpDecorate %12 BuiltIn GlobalInvocationId
                 OpDecorate %16 BuiltIn WorkgroupSize

                 OpDecorate %a_base DescriptorSet 0
                 OpDecorate %a_base Binding 0
                 OpDecorate %b_base DescriptorSet 0
                 OpDecorate %b_base Binding 1
                 OpDecorate %sneg_base DescriptorSet 0
                 OpDecorate %sneg_base Binding 2
                 OpDecorate %add_base DescriptorSet 0
                 OpDecorate %add_base Binding 3
                 OpDecorate %sub_base DescriptorSet 0
                 OpDecorate %sub_base Binding 4
                 OpDecorate %mul_base DescriptorSet 0
                 OpDecorate %mul_base Binding 5
                 OpDecorate %udiv_base DescriptorSet 0
                 OpDecorate %udiv_base Binding 6
                 OpDecorate %sdiv_base DescriptorSet 0
                 OpDecorate %sdiv_base Binding 7
                 OpDecorate %umod_base DescriptorSet 0
                 OpDecorate %umod_base Binding 8
                 OpDecorate %srem_base DescriptorSet 0
                 OpDecorate %srem_base Binding 9

            %1 = OpTypeInt 32 0
            %2 = OpTypePointer StorageBuffer %1
            %3 = OpTypeRuntimeArray %1
            %4 = OpTypeStruct %3
            %5 = OpTypePointer StorageBuffer %4
            %6 = OpTypeVoid
            %7 = OpTypeFunction %6
            %8 = OpTypeVector %1 3
            %9 = OpTypePointer Input %8
           %10 = OpTypePointer Input %1
           %11 = OpTypePointer Private %8
           %i0 = OpConstant %1 0
           %12 = OpVariable %9 Input
           %13 = OpSpecConstant %1 1
           %14 = OpSpecConstant %1 1
           %15 = OpSpecConstant %1 1
           %16 = OpSpecConstantComposite %8 %13 %14 %15
           %17 = OpVariable %11 Private %16
       %a_base = OpVariable %5 StorageBuffer
       %b_base = OpVariable %5 StorageBuffer
    %sneg_base = OpVariable %5 StorageBuffer
     %add_base = OpVariable %5 StorageBuffer
     %sub_base = OpVariable %5 StorageBuffer
     %mul_base = OpVariable %5 StorageBuffer
    %udiv_base = OpVariable %5 StorageBuffer
    %sdiv_base = OpVariable %5 StorageBuffer
    %umod_base = OpVariable %5 StorageBuffer
    %srem_base = OpVariable %5 StorageBuffer

           %18 = OpFunction %6 None %7
           %19 = OpLabel
        %i_ptr = OpAccessChain %10 %12 %i0
            %i = OpLoad %1 %i_ptr
        %a_ptr = OpAccessChain %2 %a_base %i0 %i
            %a = OpLoad %1 %a_ptr
        %b_ptr = OpAccessChain %2 %b_base %i0 %i
            %b = OpLoad %1 %b_ptr

         %sneg = OpSNegate %1 %a
     %sneg_ptr = OpAccessChain %2 %sneg_base %i0 %i
                 OpStore %sneg_ptr %sneg

          %add = OpIAdd %1 %a %b
      %add_ptr = OpAccessChain %2 %add_base %i0 %i
                 OpStore %add_ptr %add

          %sub = OpISub %1 %a %b
      %sub_ptr = OpAccessChain %2 %sub_base %i0 %i
                 OpStore %sub_ptr %sub

          %mul = OpIMul %1 %a %b
      %mul_ptr = OpAccessChain %2 %mul_base %i0 %i
                 OpStore %mul_ptr %mul

         %udiv = OpUDiv %1 %a %b
     %udiv_ptr = OpAccessChain %2 %udiv_base %i0 %i
                 OpStore %udiv_ptr %udiv

         %sdiv = OpSDiv %1 %a %b
     %sdiv_ptr = OpAccessChain %2 %sdiv_base %i0 %i
                 OpStore %sdiv_ptr %sdiv

         %umod = OpUMod %1 %a %b
     %umod_ptr = OpAccessChain %2 %umod_base %i0 %i
                 OpStore %umod_ptr %umod

         %srem = OpSRem %1 %a %b
     %srem_ptr = OpAccessChain %2 %srem_base %i0 %i
                 OpStore %srem_ptr %srem

                 OpReturn
                 OpFunctionEnd
